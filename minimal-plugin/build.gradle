import io.micronaut.internal.build.sourcegen.WriteVersions

plugins {
    id "io.micronaut.internal.build.gradle-plugin"
    id "java-test-fixtures"
}

description = "The main Micronaut Gradle plugin"

micronautPlugins {
    register('component', 'io.micronaut.gradle.MicronautComponentPlugin', 'Micronaut Component Plugin')
    register('minimal.library', 'io.micronaut.gradle.MicronautMinimalLibraryPlugin', 'Micronaut Minimal Library Plugin')
    register('minimal.application', 'io.micronaut.gradle.MicronautMinimalApplicationPlugin', 'Micronaut Minimal Application Plugin')
}

dependencies {
    compileOnly libs.bundles.optionalPlugins
    compileOnly libs.shadowPlugin

    testFixturesImplementation gradleTestKit()
    testFixturesImplementation libs.groovy.core
    testFixturesImplementation libs.spock.core
    testFixturesImplementation libs.spock.junit4
}

var writeVersions = tasks.register("writeDefaultVersions", WriteVersions) {
    outputDirectory = layout.buildDirectory.dir("generated/default-versions")
    versions.put('test-resources', libs.versions.micronaut.testresources)
    versions.put('openapi', libs.versions.micronaut.openapi)
    versions.put('graalvm', libs.versions.graalvm)
    packageName = 'io.micronaut.gradle'
}

sourceSets.main.java.srcDir(writeVersions)
