plugins {
    id 'java'
    id 'pl.droidsonroids.jacoco.testkit'
    id 'com.adarshr.test-logger'
}

dependencies {
    testImplementation libs.groovy.core
    testImplementation libs.spock.core
    testImplementation libs.spock.junit4
}

// We are NOT using the `jvm-test-suite` plugin because
// it would generate different source sets for each test
// suite, which is not what we want: we simply need different
// test tasks, with different includes

tasks.withType(Test).configureEach {
    dependsOn("generateJacocoTestKitProperties")
    useJUnitPlatform()
    def graalvmHome = providers.environmentVariable('GRAALVM_HOME')
            .orElse(providers.environmentVariable("JAVA_HOME"))
            .getOrElse("")
    inputs.property("GRAALVM_HOME", graalvmHome)
    environment "GRAALVM_HOME", graalvmHome
    systemProperty "micronautVersion", libs.versions.micronaut.platform.get()
    develocity.predictiveTestSelection {
        enabled = micronautBuild.environment.isTestSelectionEnabled()
    }
    maxParallelForks = Math.max(1, (int) (Runtime.runtime.availableProcessors()/2))
}

// Required for configuration cache serialization on JDK 25.
tasks.named("generateJacocoTestKitProperties").configure {
    doLast {
        def propsFile = outputs.files.singleFile
        def lines = propsFile.readLines('UTF-8')
        def opensArg = "--add-opens=java.base/java.lang.invoke=ALL-UNNAMED"
        def updated = lines.collect { line ->
            if (!line.startsWith("org.gradle.jvmargs=")) {
                return line
            }
            if (line.contains(opensArg)) {
                return line
            }
            // Keep the value quoted and append the additional JVM arg.
            if (line.endsWith('"')) {
                return line[0..-2] + " ${opensArg}\""
            }
            return line + " ${opensArg}"
        }
        propsFile.text = updated.join(System.lineSeparator()) + System.lineSeparator()
    }
}

testlogger {
    theme = 'standard-parallel'
    showFullStackTraces = true
    showStandardStreams = true
    showPassedStandardStreams = false
    showSkippedStandardStreams = false
    showFailedStandardStreams = true
}
