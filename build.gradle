// First, apply the publishing plugin
plugins {
  id "idea"
  id "com.gradle.plugin-publish" version "0.15.0"
  id "java-gradle-plugin"
  id "maven-publish"
  id "groovy"
  id "com.adarshr.test-logger" version "3.0.0"
  // Apply other plugins here, e.g. the kotlin plugin for a plugin written in Kotlin
  // or the groovy plugin if the plugin uses Groovy
}

// If your plugin has any external java dependencies, Gradle will attempt to
// download them from JCenter for anyone using the plugins DSL
// so you should probably use JCenter for dependency resolution in your own
// project.
repositories {
  jcenter()
}

// Unless overridden in the pluginBundle config DSL, the project version will
// be used as your plugin version when publishing
version = projectVersion
group = "io.micronaut.gradle"

// Use java-gradle-plugin to generate plugin descriptors and specify plugin ids
gradlePlugin {
  plugins {
    libraryPlugin {
      id = 'io.micronaut.library'
      implementationClass = 'io.micronaut.gradle.MicronautLibraryPlugin'
    }
    applicationPlugin {
      id = 'io.micronaut.application'
      implementationClass = 'io.micronaut.gradle.MicronautApplicationPlugin'
    }
    graalPlugin {
      id = 'io.micronaut.graalvm'
      implementationClass = 'io.micronaut.gradle.graalvm.MicronautGraalPlugin'
    }
  }
}
configurations {
  optionalPluginDependencies
}
tasks.withType(PluginUnderTestMetadata) {
  pluginClasspath.from(configurations.optionalPluginDependencies)
}

ext {
  kotlinVersion = "1.4.32"
}

dependencies {
  implementation "com.bmuschko:gradle-docker-plugin:6.7.0"
  implementation "com.diffplug.gradle:goomph:3.29.1"
  implementation "com.github.jengelman.gradle.plugins:shadow:6.1.0"
  compileOnly "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
  compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

  optionalPluginDependencies("org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion")
  optionalPluginDependencies "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  testImplementation("org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion")
  testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  testImplementation("org.codehaus.groovy:groovy:3.0.9")
  testImplementation("org.spockframework:spock-core:2.0-groovy-3.0")
  testImplementation 'org.spockframework:spock-junit4:2.0-groovy-3.0'
}

test {
  useJUnitPlatform()
}
// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
  website = 'https://micronaut.io/'
  vcsUrl = 'https://github.com/micronaut-projects/micronaut-gradle-plugins'
  description = 'Gradle Plugins for Micronaut'
  tags = ['micronaut']

  plugins {
    libraryPlugin {
      displayName = 'Micronaut Library Plugin'
    }
    applicationPlugin {
      displayName = 'Micronaut Application Plugin'
    }
    graalPlugin {
      displayName = 'Micronaut GraalVM Plugin'
    }
  }
}

java {
    sourceCompatibility = JavaVersion.toVersion('1.8')
    targetCompatibility = JavaVersion.toVersion('1.8')
}

idea.module {
  downloadJavadoc = true
  downloadSources = true
}

tasks.register("dependencyUpdates") {
  description = "Placeholder task for compatibility with other Micronaut builds"
}
